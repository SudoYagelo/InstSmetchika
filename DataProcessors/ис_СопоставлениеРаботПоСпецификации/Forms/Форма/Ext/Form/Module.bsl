   
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Объект.Токен = "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";
	
	Объект.МодельЗапроса = "gpt-4o";
	
	Объект.РазмерПакета = 1;
	
	Объект.ШаблонЗапроса = ис_НастройкиИнструментов.ШаблонЗапроса();  
	
	ИспользоватьГибкийПоискВКеше = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		"Обработка.ис_СопоставлениеРаботПоСпецификации", "ИспользоватьГибкийПоискВКеше", Истина);
							 
	// #ГК-ВДВ 20251107 {Метод и регистр лежат в расширении ВДВ_СборСобытийОткрытияЗакрытияФорм
	Попытка
		СтруктураПараметров = Новый Структура;
		СтруктураПараметров.Вставить("Владелец",	СтрШаблон("Обработка %1", "Сопоставление работ по спецификации"));
		СтруктураПараметров.Вставить("ВидСобытия",	"ОткрытиеФормы");
		Рез = гк_ОбщегоНазначения.ЗаписатьСобытиеВРегистр(СтруктураПараметров); 		
	Исключение
	КонецПопытки;
	// #ГК-ВДВ 20251107 }
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	// #ГК-ВДВ 20251107 {Метод и регистр лежат в расширении ВДВ_СборСобытийОткрытияЗакрытияФорм
	Попытка
		СтруктураПараметров = Новый Структура;
		СтруктураПараметров.Вставить("Владелец",	"Обработка Сопоставление работ по спецификации");
		СтруктураПараметров.Вставить("ВидСобытия",	"ЗакрытиеФормы");
		Рез = гк_ОбщегоНазначения.ЗаписатьСобытиеВРегистр(СтруктураПараметров); 		
	Исключение
	КонецПопытки;
	// #ГК-ВДВ 20251107 }

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ИсточникВыбора.ИмяФормы = "Обработка.ис_СопоставлениеРаботПоСпецификации.Форма.ФормаЗагрузки" Тогда
		
		Результат = ПолучитьИзВременногоХранилища(ВыбранноеЗначение.АдресВХранилище);		
		
		ЗагрузитьТабличногоДокумента(Результат);  
		
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура ПриСохраненииДанныхВНастройкахНаСервере(Настройки)
	
	СохранитьТабличнуюЧастьВНастройки(Настройки, "СписокРаботИКатегорий");
	СохранитьТабличнуюЧастьВНастройки(Настройки, "РезультатыПодбора");
	СохранитьРеквизитВНастройки(Настройки, "ПапкаРабот");

КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки) 
	
	ЗагрузитьТабличнуюЧастьИзНастроек(Настройки, "СписокРаботИКатегорий");
	ЗагрузитьТабличнуюЧастьИзНастроек(Настройки, "РезультатыПодбора");
	ЗагрузитьРеквизитИзНастроек(Настройки, "ПапкаРабот");

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПапкаРаботПриИзменении(Элемент)
	
	ЗаполнитьРаботыНаСервере();
	
КонецПроцедуры   

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовТабличнойЧастиРезультатыПодбора

&НаКлиенте
Процедура РезультатыПодбораВидРаботыПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.РезультатыПодбора.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьРеквизитыПоКлассификатору(ТекущиеДанные, ТекущиеДанные.ВидРаботы);
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовТабличнойЧастиРезультатДеревом

&НаКлиенте
Процедура РезультатДеревомКВРПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.РезультатДеревом.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;  
	
	Если ЕстьПодчиненныеЭлементы(ТекущиеДанные) Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьРеквизитыПоКлассификатору(ТекущиеДанные, ТекущиеДанные.КВР);
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатДеревомПровереноПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.РезультатДеревом.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПризнакПроверкиДляПодчиненных(ТекущиеДанные);
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиКомандФормы  

&НаКлиенте
Процедура ОбработатьПозиции(Команда)
	
	Если Объект.СписокРаботИКатегорий.Количество() = 0 Тогда
		Предупреждение("Сначала загрузите список работ и категорий!");
		Возврат; 
	Иначе
		ОчиститьКолонки();
	КонецЕсли;
	
	Если Объект.РезультатыПодбора.Количество() = 0 Тогда
		Предупреждение("Сначала загрузите файл спецификации!");
		Возврат;
	КонецЕсли;
	
	Объект.ТекстыЗапросовКИИ = "";
	
	//ОбработатьПозицииНаСервере();
	//ис_ЗаполнениеСпецификаций.ОбработатьВсеПозиции(Объект.СписокРаботИКатегорий, Объект.РезультатыПодбора, 
		//Объект.ПапкаРабот, Объект.ШаблонЗапроса, Объект.МодельЗапроса, Объект.РазмерПакета);

	ДлительнаяОперация = ВыполнитьФоновоеЗаданиеНаСервере();
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
	ПараметрыОжидания.Интервал = 2;
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(
		ДлительнаяОперация,
		Новый ОписаниеОповещения("ВыполнитьПроцедуруФоновоВыполнено", ЭтотОбъект),
		ПараметрыОжидания);	
		
КонецПроцедуры 

&НаКлиенте
Процедура ОбработатьПозицииСКешем(Команда)
	
	ОбработатьПозицииСКешемНаСервере(); 
	
КонецПроцедуры   

&НаКлиенте
Процедура ЗаполнитьКатегории(Команда)
	
	Обработка = РеквизитФормыВЗначение("Объект");
	Обработка.ЗаполнитьКатегорииНаСервере();
	ЗначениеВРеквизитФормы(Обработка, "Объект");
	
КонецПроцедуры 

&НаКлиенте
Процедура ЗагрузитьЧерезТабДок(Команда)

	ОткрытьФорму("Обработка.ис_СопоставлениеРаботПоСпецификации.Форма.ФормаЗагрузки", , 
		ЭтотОбъект, УникальныйИдентификатор);

КонецПроцедуры  

&НаКлиенте
Процедура ПеренестиВПроверку(Команда)
	
	Если ЕстьРезультатыПроверки() Тогда
		ПоказатьВопросОчисткиРезультатов();
	Иначе
		ВыполнитьПереносВПроверку();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиВОбработку(Команда)
	
	ПеренестиВОбработкуНаСервере();
	
	АктивироватьСтраницуРезультаты();
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиВЖурнал(Команда)     
	
	ВыделенныеСтроки = Элементы.РезультатыПодбора.ВыделенныеСтроки;
	
	Если ВыделенныеСтроки.Количество() = 0 Тогда
		ПоказатьПредупреждение(, "Не выбрано ни одной строки для переноса");
		Возврат;
	КонецЕсли;
	
	КоличествоПеренесено = ПеренестиВЖурналНаСервере(ВыделенныеСтроки);
	ОповеститьПользователяОРезультате(КоличествоПеренесено);
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьГибкийПоискВКешеПриИзменении(Элемент)
	
	СохранитьНастройкуГибкогоПоиска();

КонецПроцедуры

#КонецОбласти  

#Область СлужебныеПроцедурыИФункции 

#Область ЗаполнениеПоКлассификатору 

&НаКлиенте
Функция ЕстьПодчиненныеЭлементы(ТекущиеДанные)
	
	Возврат ТекущиеДанные.ПолучитьЭлементы().Количество() > 0;
	
КонецФункции

&НаКлиенте
Процедура ЗаполнитьРеквизитыПоКлассификатору(ТекущиеДанные, ВидРаботы)
	
	Если Не ЗначениеЗаполнено(ВидРаботы) Тогда
		ОчиститьСвязанныеРеквизиты(ТекущиеДанные);
		Возврат;
	КонецЕсли;
	
	РеквизитыКлассификатора = ПолучитьРеквизитыКлассификатораНаСервере(ВидРаботы);
	
	Если Не РеквизитыКлассификатора = Неопределено Тогда
		ЗаполнитьСвязанныеРеквизиты(ТекущиеДанные, РеквизитыКлассификатора);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьСвязанныеРеквизиты(ТекущиеДанные)
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТекущиеДанные, "Раздел") Тогда		
		ТекущиеДанные.Раздел = Неопределено;
	КонецЕсли; 
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТекущиеДанные, "КодификаторКВР") Тогда					
		ТекущиеДанные.КодификаторКВР = "";
	КонецЕсли;	
	
	ТекущиеДанные.КодСтатьи = "";
	ТекущиеДанные.ЕдИзмКВР = Неопределено;	
	ТекущиеДанные.ГоловнаяНоменклатура = Неопределено; 
	ТекущиеДанные.СтоимостьПрочихМатериалов = 0;
	ТекущиеДанные.ЦенаИндикатив = 0;
	ТекущиеДанные.ЦенаРабот = 0;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСвязанныеРеквизиты(ТекущиеДанные, РеквизитыКлассификатора)
	
	ЗаполнитьЗначенияСвойств(ТекущиеДанные, РеквизитыКлассификатора); 
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТекущиеДанные, "Раздел") Тогда		
		ТекущиеДанные.Раздел = РеквизитыКлассификатора.Категория;
	КонецЕсли; 
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТекущиеДанные, "КодификаторКВР") Тогда					
		ТекущиеДанные.КодификаторКВР = РеквизитыКлассификатора.Кодификатор;
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Функция ПолучитьРеквизитыКлассификатораНаСервере(ВидРаботы)
	 	
	ПереченьВидовРабот = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ВидРаботы);
	
	ТаблицаДанныхКлассификатора = ис_ЗаполнениеСпецификаций.ПолучитьДанныеКВР(ПереченьВидовРабот);
	
	Если ТаблицаДанныхКлассификатора.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли; 
	
	РеквизитыКлассификатора = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(ТаблицаДанныхКлассификатора[0]);
	
	Возврат РеквизитыКлассификатора;
	
КонецФункции

#КонецОбласти 

#Область ОбработкаФлагаПроверено

&НаКлиенте
Процедура УстановитьПризнакПроверкиДляПодчиненных(ТекущиеДанные)
	
	ПодчиненныеЭлементыДерева = ТекущиеДанные.ПолучитьЭлементы();
	
	Для Каждого ПодчиненныйЭлементДерева Из ПодчиненныеЭлементыДерева Цикл
		УстановитьПризнакПроверкиРекурсивно(ПодчиненныйЭлементДерева, ТекущиеДанные.Проверено);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПризнакПроверкиРекурсивно(СтрокаДерева, ЗначениеПроверено)
	
	СтрокаДерева.Проверено = ЗначениеПроверено;
	
	Для Каждого ПодчиненныйЭлемент Из СтрокаДерева.ПолучитьЭлементы() Цикл
		УстановитьПризнакПроверкиРекурсивно(ПодчиненныйЭлемент, ЗначениеПроверено);
	КонецЦикла;
	
КонецПроцедуры 

#КонецОбласти 

#Область УправлениеИнтерфейсом

&НаКлиенте
Процедура АктивироватьСтраницуПроверки()
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаПроверка;
	
КонецПроцедуры 

&НаКлиенте
Процедура АктивироватьСтраницуРезультаты()
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаРезультаты;
	
КонецПроцедуры


#КонецОбласти

#Область ПереносВЖурнал

&НаСервере
Функция ПеренестиВЖурналНаСервере(ВыделенныеСтроки)
	
	КоличествоПеренесено = 0;
	
	Для Каждого ИдентификаторСтроки Из ВыделенныеСтроки Цикл
		
		СтрокаРезультата = Объект.РезультатыПодбора.НайтиПоИдентификатору(ИдентификаторСтроки);
		
		Если СтрокаРезультата = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если Не СтрокаГотоваКПереносуВЖурнал(СтрокаРезультата) Тогда
			Продолжить;
		КонецЕсли;
		
		ЗаписатьВКешОтветовИИ(СтрокаРезультата);
		КоличествоПеренесено = КоличествоПеренесено + 1;
		
	КонецЦикла;
	
	Возврат КоличествоПеренесено;
	
КонецФункции

&НаСервере
Функция СтрокаГотоваКПереносуВЖурнал(СтрокаРезультата)
	
	Возврат ЗначениеЗаполнено(СтрокаРезультата.ВидРаботы);
	
КонецФункции

&НаСервере
Процедура ЗаписатьВКешОтветовИИ(СтрокаРезультата)
	
	Менеджер = РегистрыСведений.КешОтветовИИ.СоздатьМенеджерЗаписи();
	
	Менеджер.Период = ТекущаяДатаСеанса();
	Менеджер.НаименованиеПозиции = Лев(СтрокаРезультата.НаименованиеПозиции, 250);
	Менеджер.ГоловнаяНоменклатура = СтрокаРезультата.ГоловнаяНоменклатура;
	Менеджер.ЕдиницаИзмерения = СтрокаРезультата.ЕдиницаИзмерения;
	Менеджер.Завод = СтрокаРезультата.Завод;
	Менеджер.КВР = СтрокаРезультата.ВидРаботы;
	Менеджер.КодИзделия = СтрокаРезультата.КодИзделия;
	Менеджер.Раздел = СтрокаРезультата.Категория;
	Менеджер.ТипМарка = СтрокаРезультата.ТипМарка;
	
	Менеджер.Записать();
	
КонецПроцедуры

&НаКлиенте
Процедура ОповеститьПользователяОРезультате(КоличествоПеренесено)
	
	Если КоличествоПеренесено = 0 Тогда
		ТекстСообщения = "Нет записей для переноса в журнал";
	Иначе
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"Перенесено в журнал: %1", Формат(КоличествоПеренесено, "ЧН=0; ЧГ="));
	КонецЕсли;
	
	ПоказатьОповещениеПользователя("Перенос завершен", , ТекстСообщения);
	
КонецПроцедуры

#КонецОбласти  

#Область ПереносВПроверку

&НаКлиенте
Функция ЕстьРезультатыПроверки()
	
	Возврат РезультатДеревом.ПолучитьЭлементы().Количество() > 0;
	
КонецФункции

&НаКлиенте
Процедура ПоказатьВопросОчисткиРезультатов()
	
	ТекстВопроса = "Результаты предыдущей проверки будут очищены. Продолжить?";
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеОтветаНаВопросОчистки", ЭтотОбъект);
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеОтветаНаВопросОчистки(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ВыполнитьПереносВПроверку();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПереносВПроверку()
	
	ПеренестиВПроверкуНаСервере();
	АктивироватьСтраницуПроверки();
	
КонецПроцедуры 

&НаСервере
Процедура ПеренестиВПроверкуНаСервере()
	
	ДеревоГруппировки = ПолучитьДеревоГруппировкиПоНоменклатуре();
	ЗаполнитьДеревоФормы(ДеревоГруппировки);
	ОбработатьПримечанияВДереве();
	
КонецПроцедуры

&НаСервере
Функция ПолучитьДеревоГруппировкиПоНоменклатуре()
	
	Запрос = Новый Запрос;
	Запрос.Текст = ПолучитьТекстЗапросаГруппировки();
	Запрос.УстановитьПараметр("ТЗ", Объект.РезультатыПодбора.Выгрузить());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	
КонецФункции

&НаСервере
Функция ПолучитьТекстЗапросаГруппировки()
	
	Возврат
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЛОЖЬ КАК Проверено,
	|	ТЗ.НаименованиеПозиции КАК НаименованиеПозиции,
	|	ТЗ.Категория КАК Раздел,
	|	ТЗ.КодСтатьи КАК КодСтатьи,
	|	ТЗ.ВидРаботы КАК КВР,
	|	ТЗ.Кодификатор КАК КодификаторКВР,
	|	ТЗ.ГоловнаяНоменклатура КАК ГоловнаяНоменклатура,
	|	ТЗ.Уверенность КАК Уверенность,
	|	ТЗ.ЦенаРабот КАК ЦенаРабот,
	|	ТЗ.СтоимостьПрочихМатериалов КАК СтоимостьПрочихМатериалов,
	|	ТЗ.ЦенаИндикатив КАК ЦенаИндикатив,
	|	ТЗ.Примечание КАК Примечание,
	|	ТЗ.ЕдИзмКВР КАК ЕдИзмКВР,
	|	ТЗ.ЕдиницаИзмерения КАК ЕдИзмСпецификации,
	|	ТЗ.ПолученоИзКеша КАК ПолученоИзКеша
	|ПОМЕСТИТЬ ВТ_ТЗ
	|ИЗ
	|	&ТЗ КАК ТЗ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	*
	|ИЗ
	|	ВТ_ТЗ КАК ВТ_ТЗ
	|
	|ИТОГИ ПО
	|	ГоловнаяНоменклатура";
	
КонецФункции

&НаСервере
Процедура ЗаполнитьДеревоФормы(ДеревоГруппировки)
	
	КоллекцияЭлементов = РезультатДеревом.ПолучитьЭлементы();
	КоллекцияЭлементов.Очистить();
	
	ОбщегоНазначения.ЗаполнитьКоллекциюЭлементовДереваДанныхФормы(КоллекцияЭлементов, ДеревоГруппировки);
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьПримечанияВДереве()
	
	ДеревоЗначений = РеквизитФормыВЗначение("РезультатДеревом");
	ОбойтиДеревоПоставитьПримечание(ДеревоЗначений);
	
КонецПроцедуры  

&НаСервере
Процедура ОбойтиДеревоПоставитьПримечание(ДеревоЗначений)

	Для Каждого СтрокаДерева Из ДеревоЗначений.Строки Цикл
		Если ЗначениеЗаполнено(СтрокаДерева.КВР) Тогда
			Если НЕ Строка(СтрокаДерева.ЕдИзмКВР) = СтрокаДерева.ЕдИзмСпецификации Тогда
				СтрокаДерева.Примечание = СтрокаДерева.Примечание + "Различаются единицы измерения.";
			КонецЕсли;
		КонецЕсли;
	     
	    Если СтрокаДерева.Строки.Количество() > 0 Тогда
	       ОбойтиДеревоПоставитьПримечание(СтрокаДерева);
	    КонецЕсли;
	КонецЦикла;	

КонецПроцедуры

#КонецОбласти

#Область ПереносДанныхВРезультат

&НаСервере
Процедура ПеренестиВОбработкуНаСервере()
	
	ДеревоЗначений = РеквизитФормыВЗначение("РезультатДеревом");
	ОбойтиДеревоИЗаполнитьРезультат(ДеревоЗначений.Строки);
	
КонецПроцедуры

&НаСервере
Процедура ОбойтиДеревоИЗаполнитьРезультат(КоллекцияСтрок)
	
	Для Каждого СтрокаДерева Из КоллекцияСтрок Цикл
		
		Если СтрокаГотоваКПереносу(СтрокаДерева) Тогда
			ПеренестиСтрокуВРезультат(СтрокаДерева);
		КонецЕсли;
		
		Если СтрокаДерева.Строки.Количество() > 0 Тогда
			ОбойтиДеревоИЗаполнитьРезультат(СтрокаДерева.Строки);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция СтрокаГотоваКПереносу(СтрокаДерева)
	
	Возврат ЗначениеЗаполнено(СтрокаДерева.КВР) И СтрокаДерева.Проверено;
	
КонецФункции

&НаСервере
Процедура ПеренестиСтрокуВРезультат(СтрокаДерева)
	
	НайденныеСтроки = НайтиСтрокиПодбораПоНаименованию(СтрокаДерева.НаименованиеПозиции);
	
	Для Каждого СтрокаПодбора Из НайденныеСтроки Цикл
		
		Если СтрокаДерева.ЭтоРасходник Тогда
			ЗаполнитьКакРасходник(СтрокаПодбора);
		Иначе
			ЗаполнитьДанныеПозиции(СтрокаПодбора, СтрокаДерева);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция НайтиСтрокиПодбораПоНаименованию(НаименованиеПозиции)
	
	Отбор = Новый Структура("НаименованиеПозиции", НаименованиеПозиции);
	
	Возврат Объект.РезультатыПодбора.НайтиСтроки(Отбор);
	
КонецФункции

&НаСервере
Процедура ЗаполнитьКакРасходник(СтрокаПодбора)
	
	СтрокаПодбора.ВидРаботы = Неопределено;
	СтрокаПодбора.ВидРаботыНаименование = "Расходники";
	СтрокаПодбора.Уверенность = 0;
	СтрокаПодбора.Кодификатор = "";
	СтрокаПодбора.ГоловнаяНоменклатура = Неопределено;
	СтрокаПодбора.КодСтатьи = "";
	СтрокаПодбора.ЦенаИндикатив = 0;
	СтрокаПодбора.СтоимостьПрочихМатериалов = 0;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеПозиции(СтрокаПодбора, СтрокаДерева)
	
	ЗаполнитьЗначенияСвойств(СтрокаПодбора, СтрокаДерева);
	
	СтрокаПодбора.Категория = СтрокаДерева.Раздел;  
	СтрокаПодбора.Кодификатор = СтрокаДерева.КодификаторКВР;
	
	СтрокаПодбора.ВидРаботы = СтрокаДерева.КВР;
	СтрокаПодбора.ВидРаботыНаименование = Строка(СтрокаДерева.КВР); 
	СтрокаПодбора.ЕдиницаИзмерения = СтрокаДерева.ЕдИзмСпецификации;
	СтрокаПодбора.КоэффициентПересчета = СтрокаДерева.КоэффициентПересчета; 
	СтрокаПодбора.КодВидаРаботы = СтрокаДерева.КодСтатьи;
	
	ПересчитатьКоличествоПриНеобходимости(СтрокаПодбора);
	
КонецПроцедуры

&НаСервере
Процедура ПересчитатьКоличествоПриНеобходимости(СтрокаПодбора)
	
	Если СтрокаПодбора.КоэффициентПересчета = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаПодбора.КоличествоСКоэффициентомПересчета = 
		СтрокаПодбора.Количество * СтрокаПодбора.КоэффициентПересчета;
	
	ДобавитьПредупреждениеОРазличииЕдиниц(СтрокаПодбора);
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьПредупреждениеОРазличииЕдиниц(СтрокаПодбора)
	
	ТекстПредупреждения = "Различаются единицы измерения!";
	
	Если СтрЗаканчиваетсяНа(СтрокаПодбора.Примечание, ТекстПредупреждения) Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаПодбора.Примечание = СтрокаПодбора.Примечание + ТекстПредупреждения;
	
КонецПроцедуры

#КонецОбласти

#Область ЗагрузкаДанных 

&НаСервере
Процедура ЗагрузитьТабличногоДокумента(ТабличныйДокумент)

	ТаблицаДанных = ПолучитьТаблицуИзДокумента(ТабличныйДокумент);
	
	Помещение = "";
	ТипПомещения = "";
	ОбъектСтроительства = ""; 
	ПомещениеБуфер = Ложь;
	
	Сч = 0;
	
	Для каждого СтрокаТЗ Из ТаблицаДанных Цикл
		
		Если СтрокаТЗ.Позиция = "" Тогда
			
			ОбъектСтроительства = Справочники.ОбъектыСтроительства.НайтиПоРеквизиту("гк_ИДОбъекта", 
				СтрокаТЗ.НаименованиеИТехническаяХарактеристика);
			
			Сч = Сч + 1;
			
			Продолжить;   
			
		КонецЕсли;
		
		Если СтрокаТЗ.НаименованиеИТехническаяХарактеристика = "" Тогда 
			
			Если ТаблицаДанных[Сч+1].НаименованиеИТехническаяХарактеристика = "" Тогда
				Помещение = СтрокаТЗ.Позиция;
			ИначеЕсли ТаблицаДанных[Сч-1].НаименованиеИТехническаяХарактеристика = "" Тогда	
			    ТипПомещения = СтрокаТЗ.Позиция;
			Иначе
				ТипПомещения = СтрокаТЗ.Позиция;				
			КонецЕсли; 
			
		Иначе   
			
			СтрокаРезультат = Объект.РезультатыПодбора.Добавить();
			СтрокаРезультат.Позиция = СтрокаТЗ.Позиция;
			СтрокаРезультат.НаименованиеПозиции = СтрокаТЗ.НаименованиеИТехническаяХарактеристика;
			СтрокаРезультат.Помещение = Помещение;
			СтрокаРезультат.ТипПомещения = ТипПомещения;
			СтрокаРезультат.ОбъектСтроительства = ОбъектСтроительства;
			СтрокаРезультат.Количество = СтрокаТЗ.Количество;
			СтрокаРезультат.ЕдиницаИзмерения = СтрокаТЗ.ЕдиницаИзмерения;
			СтрокаРезультат.КодИзделия = СтрокаТЗ.КодИзделия;
			СтрокаРезультат.Завод = СтрокаТЗ.Завод_изготовитель;
			СтрокаРезультат.ТипМарка = СтрокаТЗ.Тип_Марка__обозначение_документа__опросногоЛиста;  
			
		КонецЕсли;
		
		Сч = Сч + 1; 
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьТаблицуИзДокумента(ТабличныйДокумент)
	
	Построитель = Новый ПостроительЗапроса;
	Построитель.ИсточникДанных = Новый ОписаниеИсточникаДанных(ТабличныйДокумент.Область());
	Построитель.Выполнить();
	
	Возврат Построитель.Результат.Выгрузить();
	
КонецФункции  

#КонецОбласти

&НаСервере
Функция ВыполнитьФоновоеЗаданиеНаСервере()
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Сопоставление с ИИ'");
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, 
		"ис_ЗаполнениеСпецификаций.ОбработатьВсеПозиции", 
		Объект.СписокРаботИКатегорий.Выгрузить(), 
		Объект.РезультатыПодбора.Выгрузить(), 
		Объект.ПапкаРабот, 
		Объект.РазмерПакета, 
		Объект.Токен,
		ИспользоватьГибкийПоискВКеше);
		
	Возврат ДлительнаяОперация;
	
КонецФункции
		
&НаКлиенте
Процедура ВыполнитьПроцедуруФоновоВыполнено(Результат, ДополнительныеПараметры) Экспорт

    Если Результат = Неопределено Тогда
        Возврат;
    ИначеЕсли Результат.Статус = "Ошибка" Тогда
        ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Результат.ПодробноеПредставлениеОшибки);
    ИначеЕсли Результат.Статус = "Выполнено" Тогда
        ОбработатьРезультат(Результат.АдресРезультата);
    КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ОбработатьРезультат(Адрес)

	Результат = ПолучитьИзВременногоХранилища(Адрес);
	
	Если Результат.РезультатПодбораОбработанный.Количество() > 0 Тогда
		Объект.РезультатыПодбора.Очистить();
		Объект.РезультатыПодбора.Загрузить(Результат.РезультатПодбораОбработанный);
	КонецЕсли;
	
	Объект.ТекстыЗапросовКИИ = ОБъект.ТекстыЗапросовКИИ + Символы.ПС + Результат.ТекстыЗапросов;

КонецПроцедуры

&НаСервере
Процедура ОчиститьКолонки()
	
	МассивИменКолонокКОчищению = Новый Массив;
	МассивИменКолонокКОчищению.Добавить("КодСтатьи");
	МассивИменКолонокКОчищению.Добавить("Категория");
	МассивИменКолонокКОчищению.Добавить("Кодификатор");
	МассивИменКолонокКОчищению.Добавить("ЦенаРабот");
	МассивИменКолонокКОчищению.Добавить("СтоимостьПрочихМатериалов");
	МассивИменКолонокКОчищению.Добавить("ЦенаИндикатив");
	МассивИменКолонокКОчищению.Добавить("Уверенность");
	МассивИменКолонокКОчищению.Добавить("ВидРаботыНаименование");
	МассивИменКолонокКОчищению.Добавить("ВидРаботы");
	МассивИменКолонокКОчищению.Добавить("ГоловнаяНоменклатура");
	
	Для каждого СтрРезультат Из Объект.РезультатыПодбора Цикл
		Для каждого ИмяКолонки Из МассивИменКолонокКОчищению Цикл
			СтрРезультат[ИмяКолонки] = Неопределено;		
		КонецЦикла;	
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ОбработатьПозицииНаСервере()
	
	Обработка = РеквизитФормыВЗначение("Объект");
	
	Обработка.ВыполнитьВФоне();
	
	ЗначениеВРеквизитФормы(Обработка, "Объект");
		
КонецПроцедуры

&НаСервере
Процедура ОбработатьПозицииСКешемНаСервере()
	
	ВремяНачала = ТекущаяУниверсальнаяДатаВМиллисекундах();
	
	Результат = ис_ЗаполнениеСпецификаций.ОбработатьВсеПозицииСКешем(
		Объект.СписокРаботИКатегорий.Выгрузить(), 
		Объект.РезультатыПодбора.Выгрузить(), 
		Объект.ПапкаРабот, 
		ИспользоватьГибкийПоискВКеше);
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТЗПодбораРезультат = Результат.РезультатПодбораОбработанный;
	
	Если ТЗПодбораРезультат.Количество() > 0 Тогда
		
		Объект.РезультатыПодбора.Очистить();
		Объект.РезультатыПодбора.Загрузить(ТЗПодбораРезультат);
		
	КонецЕсли;
	
	ВремяВыполнения = ТекущаяУниверсальнаяДатаВМиллисекундах() - ВремяНачала;
	ТекстСообщения = СтрШаблон("Время выполнения: %1 сек. (%2 мс.)", ВремяВыполнения/1000, ВремяВыполнения);
	ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРаботыНаСервере()
	
	Объект.СписокРаботИКатегорий.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	гк_КлассификаторВидовРабот.Ссылка КАК ВидРаботы,
		|	гк_КлассификаторВидовРабот.СтатьяБДР КАК Категория,
		|	гк_КлассификаторВидовРабот.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	СОКРЛП(гк_КлассификаторВидовРабот.Наименование) КАК ВидРаботыНаименование,
		|	гк_КлассификаторВидовРабот.СтатьяБДР.Наименование КАК КатегорияНаименование,
		|	гк_КлассификаторВидовРабот.ЕдиницаИзмерения.Наименование КАК ЕдиницаИзмеренияНаименование
		|ИЗ
		|	Справочник.гк_КлассификаторВидовРабот КАК гк_КлассификаторВидовРабот
		|ГДЕ
		|	НЕ гк_КлассификаторВидовРабот.ПометкаУдаления
		|	И гк_КлассификаторВидовРабот.Ссылка В ИЕРАРХИИ(&Группа)
		|	И НЕ гк_КлассификаторВидовРабот.ЭтоГруппа
		|	И гк_КлассификаторВидовРабот.Кодификатор ПОДОБНО ""%.000""";
	
	Запрос.УстановитьПараметр("Группа", Объект.ПапкаРабот);
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	Объект.СписокРаботИКатегорий.Загрузить(РезультатЗапроса);
	
КонецПроцедуры 

&НаСервере
Процедура СохранитьНастройкуГибкогоПоиска()
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("Обработка.ис_СопоставлениеРаботПоСпецификации", 
		"ИспользоватьГибкийПоискВКеше", ИспользоватьГибкийПоискВКеше);
	
КонецПроцедуры

#Область РаботаСНастройками

&НаСервере
Процедура СохранитьТабличнуюЧастьВНастройки(Настройки, ИмяТабличнойЧасти)
	
	КлючНастройки = ПолучитьКлючНастройки(ИмяТабличнойЧасти);
	ТаблицаЗначений = Объект[ИмяТабличнойЧасти].Выгрузить();
	
	Настройки.Вставить(КлючНастройки, ТаблицаЗначений);
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьТабличнуюЧастьИзНастроек(Настройки, ИмяТабличнойЧасти)
	
	КлючНастройки = ПолучитьКлючНастройки(ИмяТабличнойЧасти);
	
	ТаблицаЗначений = Настройки.Получить(КлючНастройки);

	Если ТаблицаЗначений = Неопределено Тогда
	    Возврат;
	КонецЕсли;
	
	Если Не ТипЗнч(ТаблицаЗначений) = Тип("ТаблицаЗначений") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не СтруктураТабличнойЧастиСовпадает(ТаблицаЗначений, ИмяТабличнойЧасти) Тогда
		Возврат;
	КонецЕсли;
	
	Объект[ИмяТабличнойЧасти].Загрузить(ТаблицаЗначений);
	
КонецПроцедуры

&НаСервере
Функция СтруктураТабличнойЧастиСовпадает(ТаблицаЗначений, ИмяТабличнойЧасти)
	
	ТабличнаяЧасть = Объект[ИмяТабличнойЧасти];  
	
	СтруктураКолонокСовпадает = Истина;
	
	Для Каждого Колонка Из ТаблицаЗначений.Колонки Цикл
		
		Если ТабличнаяЧасть.Выгрузить().Колонки.Найти(Колонка.Имя) = Неопределено Тогда
			
			СтруктураКолонокСовпадает = Ложь;
			
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат СтруктураКолонокСовпадает;
	
КонецФункции

&НаСервере
Процедура СохранитьРеквизитВНастройки(Настройки, ИмяРеквизита)
	
	КлючНастройки = ПолучитьКлючНастройки(ИмяРеквизита);
	ЗначениеРеквизита = Объект[ИмяРеквизита];
	
	Настройки.Вставить(КлючНастройки, ЗначениеРеквизита);
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьРеквизитИзНастроек(Настройки, ИмяРеквизита)
	
	КлючНастройки = ПолучитьКлючНастройки(ИмяРеквизита);
	
	ЗначениеРеквизита = Настройки.Получить(КлючНастройки);

	Если ЗначениеРеквизита = Неопределено Тогда
	    Возврат;
	КонецЕсли;
	
	Объект[ИмяРеквизита] = ЗначениеРеквизита;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьКлючНастройки(ИмяРеквизита)
	
	Возврат "Настройка_" + ИмяРеквизита;
	
КонецФункции

#КонецОбласти

#КонецОбласти

