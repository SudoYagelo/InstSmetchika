#Область ПрограммныйИнтерфейс

// Выполняет пакетную обработку позиций спецификации с использованием AI-сервиса и кеша
//
// Параметры:
//  СписокРаботИКатегорий - ТаблицаЗначений - список работ и категорий для сопоставления:
//   * ВидРаботы - СправочникСсылка.гк_КлассификаторВидовРабот - вид работы
//   * Категория - СправочникСсылка.гк_СтатьиБДДС - категория работы
//  РезультатыПодбора - ТаблицаЗначений - позиции для обработки:
//   * НаименованиеПозиции - Строка - наименование позиции
//   * ТипМарка - Строка - тип/марка материала
//   * ЕдиницаИзмерения - СправочникСсылка.ЕдиницыИзмерения - единица измерения
//   * Завод - Строка - завод-изготовитель
//   * КодИзделия - Строка - код изделия
//  ПапкаРабот - СправочникСсылка.гк_КлассификаторВидовРабот - группа классификатора для отбора
//  РазмерПакетаОбработки - Число - количество позиций в одном пакете для отправки в AI
//  Токен - Строка - токен авторизации для AI-сервиса.
//                   Если не указан, используется токен из настроек.
//                   Значение по умолчанию: Неопределено
//  ИспользоватьГибкийПоискВКеше - Булево - использовать многоуровневый поиск в кеше:
//                                          Истина - поиск по всем уровням (4→3→2→1 параметр);
//                                          Ложь - только точное совпадение (4 параметра).
//                                          Значение по умолчанию: Истина
//
// Возвращаемое значение:
//  Структура - результат обработки:
//   * ТекстыЗапросов - Строка - накопленные тексты всех запросов к AI-сервису
//   * РезультатПодбораОбработанный - ТаблицаЗначений - результаты обработки с заполненными полями:
//      ** ВидРаботы - СправочникСсылка.гк_КлассификаторВидовРабот - сопоставленный вид работы
//      ** Категория - СправочникСсылка.гк_СтатьиБДДС - категория работы
//      ** ГоловнаяНоменклатура - СправочникСсылка.Номенклатура - головная номенклатура
//      ** Уверенность - Число - уровень уверенности в сопоставлении (от 0.7 до 1.0)
//
// Пример:
//  Результат = ис_ЗаполнениеСпецификаций.ОбработатьВсеПозиции(
//      СписокРабот,
//      ТаблицаПозиций,
//      ПапкаКлассификатора,
//      50,
//      "sk-...",
//      Истина
//  );
//  Если Результат <> Неопределено Тогда
//      ОбработанныеПозиции = Результат.РезультатПодбораОбработанный;
//  КонецЕсли;
//
Функция ОбработатьВсеПозиции(СписокРаботИКатегорий, РезультатыПодбора, ПапкаРабот,
		РазмерПакетаОбработки, Токен = Неопределено, ИспользоватьГибкийПоискВКеше = Истина) Экспорт
	
	// Валидация входных параметров
	ВалидироватьВходныеПараметры(СписокРаботИКатегорий, РезультатыПодбора, ПапкаРабот);
	
	// Подготовка данных
	ДанныеДляОбработки = ПодготовитьДанныеДляОбработки(
		СписокРаботИКатегорий, 
		РезультатыПодбора, 
		ПапкаРабот, ИспользоватьГибкийПоискВКеше);
	
	РезультатПодбораОбработанный = РезультатыПодбора.Скопировать();
	ТекстыЗапросовКИИ = "";
	
	// Обработка позиций с кешем
	ОбработатьПозицииСКешом(
		ДанныеДляОбработки.ПозицииСКешом,
		СписокРаботИКатегорий,
		ДанныеДляОбработки.КатегорияДляРасходников,
		РезультатПодбораОбработанный,
		ДанныеДляОбработки.ИндексКеша,
		ДанныеДляОбработки.ДанныеВсехПозицийКВР);
	
	// Обработка позиций через AI
	Если ДанныеДляОбработки.ПозицииБезКеша.Количество() > 0 Тогда
		ТекстыЗапросовКИИ = ОбработатьПозицииЧерезИИ(
			ДанныеДляОбработки.ПозицииБезКеша,
			СписокРаботИКатегорий,
			ДанныеДляОбработки.МассивКатегорий,
			ПапкаРабот,
			ДанныеДляОбработки.КатегорияДляРасходников,
			РезультатПодбораОбработанный,
			ДанныеДляОбработки.ДанныеВсехПозицийКВР,
			РазмерПакетаОбработки,
			ДанныеДляОбработки.КоличествоПозиций
		);
	КонецЕсли;

	Результат = Новый Структура;
	Результат.Вставить("ТекстыЗапросов", ТекстыЗапросовКИИ);
	Результат.Вставить("РезультатПодбораОбработанный", РезультатПодбораОбработанный);
	
	Возврат Результат;
	
КонецФункции

// Выполняет обработку позиций спецификации только с использованием кеша (без обращения к AI)
//
// Параметры:
//  СписокРаботИКатегорий - ТаблицаЗначений - список работ и категорий для сопоставления:
//   * ВидРаботы - СправочникСсылка.гк_КлассификаторВидовРабот - вид работы
//   * Категория - СправочникСсылка.гк_СтатьиБДДС - категория работы
//  РезультатыПодбора - ТаблицаЗначений - позиции для обработки:
//   * НаименованиеПозиции - Строка - наименование позиции
//   * ТипМарка - Строка - тип/марка материала
//   * ЕдиницаИзмерения - СправочникСсылка.ЕдиницыИзмерения - единица измерения
//   * Завод - Строка - завод-изготовитель
//   * КодИзделия - Строка - код изделия
//  ПапкаРабот - СправочникСсылка.гк_КлассификаторВидовРабот - группа классификатора для отбора
//  ИспользоватьГибкийПоискВКеше - Булево - использовать многоуровневый поиск в кеше:
//                                          Истина - поиск по всем уровням (4→3→2→1 параметр);
//                                          Ложь - только точное совпадение (4 параметра).
//                                          Значение по умолчанию: Истина
//
// Возвращаемое значение:
//  Структура - результат обработки:
//   * РезультатПодбораОбработанный - ТаблицаЗначений - результаты обработки с заполненными полями:
//      ** ВидРаботы - СправочникСсылка.гк_КлассификаторВидовРабот - сопоставленный вид работы
//      ** Категория - СправочникСсылка.гк_СтатьиБДДС - категория работы
//      ** ГоловнаяНоменклатура - СправочникСсылка.Номенклатура - головная номенклатура
//      ** Уверенность - Число - уровень уверенности в сопоставлении (от 0.7 до 1.0)
//  Неопределено - если не удалось выполнить обработку
//
// Примечание:
//  Функция обрабатывает только те позиции, для которых найдены совпадения в кеше.
//  Позиции без совпадений остаются незаполненными. Для полной обработки используйте
//  функцию ОбработатьВсеПозиции(), которая дополнительно обращается к AI-сервису.
//
// Пример:
//  Результат = ис_ЗаполнениеСпецификаций.ОбработатьВсеПозицииСКешем(
//      СписокРабот,
//      ТаблицаПозиций,
//      ПапкаКлассификатора,
//      Истина
//  );
//  Если Результат <> Неопределено Тогда
//      ОбработанныеПозиции = Результат.РезультатПодбораОбработанный;
//  КонецЕсли;
//
Функция ОбработатьВсеПозицииСКешем(СписокРаботИКатегорий, РезультатыПодбора, ПапкаРабот, 
		ИспользоватьГибкийПоискВКеше = Истина) Экспорт
		
	// Валидация входных параметров
	ВалидироватьВходныеПараметры(СписокРаботИКатегорий, РезультатыПодбора, ПапкаРабот);
	
	// Подготовка данных
	ДанныеДляОбработки = ПодготовитьДанныеДляОбработки(
		СписокРаботИКатегорий, 
		РезультатыПодбора, 
		ПапкаРабот, ИспользоватьГибкийПоискВКеше);
	
	РезультатПодбораОбработанный = РезультатыПодбора.Скопировать();
	ТекстыЗапросовКИИ = "";
	
	// Обработка позиций с кешем
	ОбработатьПозицииСКешом(
		ДанныеДляОбработки.ПозицииСКешом,
		СписокРаботИКатегорий,
		ДанныеДляОбработки.КатегорияДляРасходников,
		РезультатПодбораОбработанный,
		ДанныеДляОбработки.ИндексКеша,
		ДанныеДляОбработки.ДанныеВсехПозицийКВР);

	Результат = Новый Структура;
	Результат.Вставить("ТекстыЗапросов", "");
	Результат.Вставить("РезультатПодбораОбработанный", РезультатПодбораОбработанный);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ВалидацияИПодготовкаДанных

// Валидирует входные параметры перед обработкой позиций
//
// Параметры:
//  СписокРаботИКатегорий - ТаблицаЗначений - список работ и категорий для сопоставления
//  РезультатыПодбора     - ТаблицаЗначений - позиции для обработки
//  ПапкаРабот            - СправочникСсылка.гк_КлассификаторВидовРабот - группа классификатора
//  МодельЗапроса         - Строка - идентификатор модели AI-сервиса
//
Процедура ВалидироватьВходныеПараметры(СписокРаботИКатегорий, РезультатыПодбора, ПапкаРабот)
	
	Если СписокРаботИКатегорий.Количество() = 0 Тогда
		ВызватьИсключение "Не загружен список работ и категорий!";
	КонецЕсли;
	
	Если РезультатыПодбора.Количество() = 0 Тогда
		ВызватьИсключение "Не загружена спецификация для обработки!";
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПапкаРабот) Тогда
		ВызватьИсключение "Не указана папка работ!";
	КонецЕсли;
	
КонецПроцедуры

// Подготавливает все необходимые данные для обработки позиций
//
// Параметры:
//  СписокРаботИКатегорий - ТаблицаЗначений - список работ и категорий для сопоставления
//  РезультатыПодбора     - ТаблицаЗначений - позиции для обработки
//  ПапкаРабот            - СправочникСсылка.гк_КлассификаторВидовРабот - группа классификатора
//
// Возвращаемое значение:
//  Структура - содержит подготовленные данные:
//   * КоличествоПозиций        - Число - количество уникальных позиций
//   * МассивКатегорий          - Массив - уникальные категории работ
//   * ИндексКеша               - Соответствие - индекс для быстрого поиска в кеше
//   * КатегорияДляРасходников  - СправочникСсылка.гк_СтатьиБДДС - категория для расходных материалов
//   * ДанныеВсехПозицийКВР     - ТаблицаЗначений - данные классификатора видов работ
//   * ПозицииБезКеша           - Массив - позиции, требующие обработки через AI
//   * ПозицииСКешом            - Массив - позиции, имеющиеся в кеше
//
Функция ПодготовитьДанныеДляОбработки(СписокРаботИКатегорий, РезультатыПодбора, ПапкаРабот, ИспользоватьГибкийПоискВКеше)
	
	Результат = Новый Структура;
	
	// Создаем таблицу уникальных позиций
	ТЗУникальныеПозиции = РезультатыПодбора.Скопировать();
	ТЗУникальныеПозиции.Свернуть("НаименованиеПозиции, ТипМарка, ЕдиницаИзмерения, Завод, КодИзделия");
	
	Результат.Вставить("КоличествоПозиций", ТЗУникальныеПозиции.Количество());
	
	// Получаем массив категорий
	МассивКатегорий = ОбщегоНазначенияКлиентСервер.СвернутьМассив(
		СписокРаботИКатегорий.ВыгрузитьКолонку("Категория"));
		
	Результат.Вставить("МассивКатегорий", МассивКатегорий);

	// Получаем таблицу кеша и создаем индекс
	// ВАЖНО: Кеш фильтруется по массиву разделов из СписокРаботИКатегорий,
	// чтобы искать только в тех категориях работ, которые есть в текущем списке
	ТаблицаКеша = ПолучитьТаблицуКеша(ТЗУникальныеПозиции, МассивКатегорий);
	ИндексКеша = СоздатьИндексКеша(ТаблицаКеша, ИспользоватьГибкийПоискВКеше);
	Результат.Вставить("ИндексКеша", ИндексКеша);
	
	// Получаем категорию для расходников
	КатегорияДляРасходников = ПолучитьКатегориюДляРасходников(ПапкаРабот);
	Результат.Вставить("КатегорияДляРасходников", КатегорияДляРасходников);
	
	// Получаем данные КВР
	ДанныеВсехПозицийКВР = ПолучитьДанныеКВР(СписокРаботИКатегорий.ВыгрузитьКолонку("ВидРаботы"));
	Результат.Вставить("ДанныеВсехПозицийКВР", ДанныеВсехПозицийКВР);
	
	// Разделяем позиции на с кешом и без кеша
	РазделениеПозиций = РазделитьПозицииПоКешу(ТЗУникальныеПозиции, ИндексКеша);
	Результат.Вставить("ПозицииБезКеша", РазделениеПозиций.ПозицииБезКеша);
	Результат.Вставить("ПозицииСКешом", РазделениеПозиций.ПозицииСКешом);
	
	Возврат Результат;
	
КонецФункции

// Создает многоуровневый индекс для быстрого поиска записей в таблице кеша
//
// Параметры:
//  ТаблицаКеша - ТаблицаЗначений - таблица с кешированными ответами AI
//  ИспользоватьГибкийПоискВКеше - Булево - создавать индексы для всех уровней поиска.
//                                          Истина - создаются индексы для 4 уровней;
//                                          Ложь - только для точного совпадения (уровень 1).
//                                          Значение по умолчанию: Истина
//
// Возвращаемое значение:
//  Структура - индексы кеша с разными уровнями приоритета:
//   * Уровень1 - Соответствие - индекс по 4 параметрам (НаименованиеПозиции|Завод|КодИзделия|ТипМарка)
//   * Уровень2 - Соответствие - индекс по 3 параметрам (НаименованиеПозиции|КодИзделия|ТипМарка) - только при гибком поиске
//   * Уровень3 - Соответствие - индекс по 2 параметрам (НаименованиеПозиции|ТипМарка) - только при гибком поиске
//   * Уровень4 - Соответствие - индекс по 1 параметру (НаименованиеПозиции) - только при гибком поиске
//
Функция СоздатьИндексКеша(ТаблицаКеша, ИспользоватьГибкийПоискВКеше = Истина)
	
	ИндексКеша = Новый Структура;
	ИндексКеша.Вставить("Уровень1", Новый Соответствие); // 4 параметра - уверенность 1.0
	
	// Дополнительные уровни создаются только для гибкого поиска
	Если ИспользоватьГибкийПоискВКеше Тогда
		ИндексКеша.Вставить("Уровень2", Новый Соответствие); // 3 параметра - уверенность 0.9
		ИндексКеша.Вставить("Уровень3", Новый Соответствие); // 2 параметра - уверенность 0.8
		ИндексКеша.Вставить("Уровень4", Новый Соответствие); // 1 параметр - уверенность 0.7
	КонецЕсли;
	
	Для Каждого СтрокаКеша Из ТаблицаКеша Цикл
		
		// Уровень 1: НаименованиеПозиции + Завод + КодИзделия + ТипМарка
		// Создается всегда, независимо от режима поиска
		Ключ1 = ПолучитьКлючПозиции(СтрокаКеша.НаименованиеПозиции, СтрокаКеша.Завод, СтрокаКеша.КодИзделия, 
			СтрокаКеша.ТипМарка);
		ИндексКеша.Уровень1.Вставить(Ключ1, СтрокаКеша);
		
		// Дополнительные уровни создаются только для гибкого поиска
		Если Не ИспользоватьГибкийПоискВКеше Тогда 
			Продолжить;			
		КонецЕсли;
			
		// Уровень 2: НаименованиеПозиции + КодИзделия + ТипМарка
		Ключ2 = ПолучитьКлючПозиции(СтрокаКеша.НаименованиеПозиции, "", СтрокаКеша.КодИзделия, СтрокаКеша.ТипМарка);
		
		Если ИндексКеша.Уровень2.Получить(Ключ2) = Неопределено Тогда
			ИндексКеша.Уровень2.Вставить(Ключ2, СтрокаКеша);
		КонецЕсли;
		
		// Уровень 3: НаименованиеПозиции + ТипМарка
		Ключ3 = ПолучитьКлючПозиции(СтрокаКеша.НаименованиеПозиции, "", "", СтрокаКеша.ТипМарка);
		
		Если ИндексКеша.Уровень3.Получить(Ключ3) = Неопределено Тогда
			ИндексКеша.Уровень3.Вставить(Ключ3, СтрокаКеша);
		КонецЕсли;
		
		// Уровень 4: НаименованиеПозиции
		Ключ4 = ПолучитьКлючПозиции(СтрокаКеша.НаименованиеПозиции,	"", "", "");
		
		Если ИндексКеша.Уровень4.Получить(Ключ4) = Неопределено Тогда
			ИндексКеша.Уровень4.Вставить(Ключ4, СтрокаКеша);
		КонецЕсли;
							
	КонецЦикла;
	
	Возврат ИндексКеша;
	
КонецФункции

// Формирует уникальный ключ для позиции
//
// Параметры:
//  НаименованиеПозиции - Строка
//  Завод               - Строка
//  КодИзделия          - Строка
//  ТипМарка            - Строка
//
// Возвращаемое значение:
//  Строка - составной ключ
//
Функция ПолучитьКлючПозиции(НаименованиеПозиции, Завод, КодИзделия, ТипМарка)
	
	НаименованиеНорм = ?(ПустаяСтрока(НаименованиеПозиции), "", СокрЛП(НаименованиеПозиции));
	ЗаводНорм = ?(ПустаяСтрока(Завод), "", СокрЛП(Завод));
	КодНорм = ?(ПустаяСтрока(КодИзделия), "", СокрЛП(КодИзделия));
	МаркаНорм = ?(ПустаяСтрока(ТипМарка), "", СокрЛП(ТипМарка));
	
	Возврат СтрШаблон("%1|%2|%3|%4", НаименованиеНорм, ЗаводНорм, КодНорм, МаркаНорм);
	
КонецФункции

// Разделяет позиции на найденные в кеше и требующие обработки через AI
//
// Параметры:
//  ТЗУникальныеПозиции - ТаблицаЗначений - уникальные позиции
//  ИндексКеша          - Структура - многоуровневый индекс кеша
//
// Возвращаемое значение:
//  Структура:
//   * ПозицииСКешом  - Массив - позиции, найденные в кеше
//   * ПозицииБезКеша - Массив - позиции, требующие обработки через AI
//
Функция РазделитьПозицииПоКешу(ТЗУникальныеПозиции, ИндексКеша)
	
	ПозицииСКешом = Новый Массив;
	ПозицииБезКеша = Новый Массив;
	
	Для Каждого Позиция Из ТЗУникальныеПозиции Цикл
		
		РезультатПоиска = НайтиВКеше(
			Позиция.НаименованиеПозиции,
			Позиция.Завод,
			Позиция.КодИзделия,
			Позиция.ТипМарка,
			ИндексКеша);
		
		Если Не РезультатПоиска.НайденаСтрока = Неопределено Тогда
			ПозицииСКешом.Добавить(Позиция);
		Иначе
			ПозицииБезКеша.Добавить(Позиция);
		КонецЕсли;
		
	КонецЦикла;
	
	Результат = Новый Структура;
	Результат.Вставить("ПозицииСКешом", ПозицииСКешом);
	Результат.Вставить("ПозицииБезКеша", ПозицииБезКеша);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ОбработкаПозиций

// Процедура ОбработатьПозицииСКешом
// Обработка позиций, найденных в кеше
//
// Параметры:
//  ПозицииСКешом               - Массив - позиции для обработки
//  СписокРаботИКатегорий       - ТаблицаЗначений
//  КатегорияДляРасходников     - СправочникСсылка
//  РезультатПодбораОбработанный - ТаблицаЗначений
//  ИндексКеша                  - Структура - многоуровневый индекс
//  ДанныеВсехПозицийКВР        - ТаблицаЗначений
//
Процедура ОбработатьПозицииСКешом(ПозицииСКешом, СписокРаботИКатегорий, КатегорияДляРасходников, 
	РезультатПодбораОбработанный, ИндексКеша, ДанныеВсехПозицийКВР)
	
	Для Каждого Позиция Из ПозицииСКешом Цикл
		
		// Умный поиск в кеше с приоритетами
		РезультатПоиска = НайтиВКеше(Позиция.НаименованиеПозиции, Позиция.Завод, Позиция.КодИзделия, Позиция.ТипМарка,
			ИндексКеша);
		
		Если РезультатПоиска.НайденаСтрока <> Неопределено Тогда
			
			СтрокаКеша = РезультатПоиска.НайденаСтрока;
			
			// Заполняем данные из кеша
			ДанныеДляЗаполнения = Новый Структура;
			ДанныеДляЗаполнения.Вставить("КВР", СтрокаКеша.КВР);
			ДанныеДляЗаполнения.Вставить("Раздел", СтрокаКеша.Раздел);
			ДанныеДляЗаполнения.Вставить("ГоловнаяНоменклатура", СтрокаКеша.ГоловнаяНоменклатура);
			ДанныеДляЗаполнения.Вставить("ЕдиницаИзмерения", СтрокаКеша.ЕдиницаИзмерения);
			ДанныеДляЗаполнения.Вставить("confidence", РезультатПоиска.Уверенность);
			
			ЗаполнитьРезультатыПодбора(Позиция, ДанныеДляЗаполнения, СписокРаботИКатегорий, КатегорияДляРасходников,
				РезультатПодбораОбработанный, СтрокаКеша, ДанныеВсехПозицийКВР);	
				
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Обрабатывает позиции через AI-сервис с пакетной отправкой запросов
//
// Параметры:
//  МассивПозицийБезКеша        - Массив - позиции для обработки через AI
//  СписокРаботИКатегорий       - ТаблицаЗначений - список работ и категорий
//  МассивКатегорий             - Массив - уникальные категории работ
//  ПапкаРабот                  - СправочникСсылка.гк_КлассификаторВидовРабот - группа классификатора
//  КатегорияДляРасходников     - СправочникСсылка.гк_СтатьиБДДС - категория для расходных материалов
//  РезультатПодбораОбработанный - ТаблицаЗначений - таблица результатов (заполняется функцией)
//  ДанныеВсехПозицийКВР        - ТаблицаЗначений - данные классификатора видов работ
//  МодельЗапроса               - Строка - идентификатор модели AI-сервиса
//  РазмерПакетаОбработки       - Число - количество позиций в одном пакете
//  КоличествоПозиций           - Число - общее количество позиций (для расчета прогресса)
//  Токен                       - Строка - токен авторизации для AI-сервиса (необязательный)
//
// Возвращаемое значение:
//  Строка - накопленные тексты запросов и ответов AI для логирования
//
Функция ОбработатьПозицииЧерезИИ(МассивПозицийБезКеша, СписокРаботИКатегорий, МассивКатегорий,
	ПапкаРабот, КатегорияДляРасходников, РезультатПодбораОбработанный, ДанныеВсехПозицийКВР,
	РазмерПакетаОбработки, КоличествоПозиций)
	
	ТекстыЗапросовКИИ = "";
	
	// Разбиваем на пакеты
	МассивПакетов = РазбитьНаПакеты(МассивПозицийБезКеша, РазмерПакетаОбработки);
	
	СчПакетов = 1;
	Для Каждого ПакетПозиций Из МассивПакетов Цикл
		
		Попытка
			// Формируем пакетный запрос к AI
			ТекстПакетногоЗапроса = СформироватьЗапросДляИИ(
				ПакетПозиций, 
				СписокРаботИКатегорий, 
				МассивКатегорий, 
				ПапкаРабот
			);
			
			// Отправляем пакетный запрос
			РезультатИИ = ОтправитьЗапросChatGPT(ТекстПакетногоЗапроса);
			
			Если РезультатИИ <> Неопределено Тогда 
				РезультатИИJSON = гк_КоннекторHTTP.ОбъектВJson(РезультатИИ);
				ТекстыЗапросовКИИ = ТекстыЗапросовКИИ + ТекстПакетногоЗапроса + Символы.ПС 
					+ "Ответ ИИ: " + РезультатИИJSON + Символы.ПС;
				
				Если Не РезультатИИ.Свойство("results") Или РезультатИИ.results.Количество() = 0 Тогда
					ОбработатьОшибкуПакета(ПакетПозиций, "Ошибка получения ответа от ИИ");
				Иначе
					// Обрабатываем результаты пакета
					ОбработатьПакетныйОтветИИ(
						ПакетПозиций, 
						РезультатИИ.results, 
						СписокРаботИКатегорий, 
						КатегорияДляРасходников, 
						РезультатПодбораОбработанный, 
						ДанныеВсехПозицийКВР
					);
				КонецЕсли; 
			Иначе
				ОбработатьОшибкуПакета(ПакетПозиций, "Ошибка запроса к ИИ");
			КонецЕсли;
			
		Исключение
			ТекстОшибки = "Критическая ошибка при обработке пакета: " + ОписаниеОшибки();
			ОбработатьОшибкуПакета(ПакетПозиций, ТекстОшибки);
			ЗаписатьВЖурналРегистрации(ТекстОшибки);
		КонецПопытки;
		
		// Обновляем прогресс
		ПроцентВыполнения = Окр((СчПакетов * РазмерПакетаОбработки / КоличествоПозиций) * 100, 0);
		ПроцентВыполнения = ?(ПроцентВыполнения > 100, 100, ПроцентВыполнения);
		
		ДлительныеОперации.СообщитьПрогресс(ПроцентВыполнения, "Обработка позиций...");
		
		СчПакетов = СчПакетов + 1;
		
	КонецЦикла;
	
	Возврат ТекстыЗапросовКИИ;
	
КонецФункции

// Обрабатывает ошибку для всех позиций в пакете
//
// Параметры:
//  ПакетПозиций - Массив - позиции, для которых произошла ошибка
//  ТекстОшибки  - Строка - текст сообщения об ошибке
//
Процедура ОбработатьОшибкуПакета(ПакетПозиций, ТекстОшибки)
	
	Для Каждого ПозицияВПакете Из ПакетПозиций Цикл
		СообщениеОшибки = СтрШаблон("%1: %2", ТекстОшибки, ПозицияВПакете.НаименованиеПозиции);
		ОбщегоНазначения.СообщитьПользователю(СообщениеОшибки);
	КонецЦикла;
	
КонецПроцедуры

// Обрабатывает пакетный ответ от AI и заполняет результаты подбора
//
// Параметры:
//  ПакетПозиций                - Массив - позиции, отправленные в AI
//  РезультатыИИ                - Массив - массив ответов от AI (в том же порядке, что и позиции)
//  СписокРаботИКатегорий       - ТаблицаЗначений - список работ и категорий
//  КатегорияДляРасходников     - СправочникСсылка.гк_СтатьиБДДС - категория для расходных материалов
//  РезультатПодбораОбработанный - ТаблицаЗначений - таблица результатов (заполняется процедурой)
//  ДанныеВсехПозицийКВР        - ТаблицаЗначений - данные классификатора видов работ
//
Процедура ОбработатьПакетныйОтветИИ(ПакетПозиций, РезультатыИИ, СписокРаботИКатегорий, 
	КатегорияДляРасходников, РезультатПодбораОбработанный, ДанныеВсехПозицийКВР)
	
	// Предполагаем, что AI возвращает массив результатов в том же порядке, что и запрос
	Для Сч = 0 По ПакетПозиций.ВГраница() Цикл
		Позиция = ПакетПозиций[Сч];
		
		Если Сч < РезультатыИИ.Количество() Тогда
			РезультатИИДляПозиции = РезультатыИИ[Сч];
			ЗаполнитьРезультатыПодбора(
				Позиция, 
				РезультатИИДляПозиции, 
				СписокРаботИКатегорий, 
				КатегорияДляРасходников, 
				РезультатПодбораОбработанный, 
				Неопределено, 
				ДанныеВсехПозицийКВР
			);
		Иначе
			// Если результатов меньше чем позиций - обрабатываем ошибку
			ОбщегоНазначения.СообщитьПользователю(
				"Не получен ответ ИИ для позиции: " + Позиция.НаименованиеПозиции
			);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Заполняет результаты подбора данными из кеша или от AI
//
// Параметры:
//  ПозицияДляОбработки         - СтрокаТаблицыЗначений - обрабатываемая позиция
//  ДанныеДляЗаполнения         - Структура, СправочникСсылка - данные для заполнения (от AI или ссылка на КВР из кеша)
//  СписокРаботИКатегорий       - ТаблицаЗначений - список работ и категорий
//  КатегорияДляРасходников     - СправочникСсылка.гк_СтатьиБДДС - категория для расходных материалов
//  РезультатПодбораОбработанный - ТаблицаЗначений - таблица результатов (заполняется процедурой)
//  СтрокаКеша                  - СтрокаТаблицыЗначений, Неопределено - строка из кеша (если есть)
//  ДанныеВсехПозицийКВР        - ТаблицаЗначений - данные классификатора видов работ
//
Процедура ЗаполнитьРезультатыПодбора(ПозицияДляОбработки, ДанныеДляЗаполнения, СписокРаботИКатегорий, 
	КатегорияДляРасходников, РезультатПодбораОбработанный, СтрокаКеша = Неопределено, 
	ДанныеВсехПозицийКВР = Неопределено)
	
	Попытка
		// Определяем тип данных и получаем строку КВР
		СтрокаДанныхКВР = ПолучитьСтрокуДанныхКВР(ДанныеДляЗаполнения, ДанныеВсехПозицийКВР);
		
		
		Если СтрокаДанныхКВР = Неопределено Тогда
			ЗаписатьВЖурналРегистрации(
				СтрШаблон("Не найдены данные КВР для позиции: %1", ПозицияДляОбработки.НаименованиеПозиции)
			);
			//Возврат;
		КонецЕсли;
		
		// Определяем уверенность
		Уверенность = ОпределитьУверенность(ДанныеДляЗаполнения, СтрокаКеша);
		
		// Находим соответствующие строки в результатах
		Отбор = СоздатьОтборПоПозиции(ПозицияДляОбработки);
		МассивСоответствующихСтрок = РезультатПодбораОбработанный.НайтиСтроки(Отбор);
		
		// Заполняем результаты
		Для Каждого СтрокаПозиции Из МассивСоответствующихСтрок Цикл
			
			Если СтрокаКеша <> Неопределено И НЕ СтрокаДанныхКВР = Неопределено Тогда
				ЗаполнитьИзКеша(СтрокаПозиции, СтрокаКеша, СтрокаДанныхКВР, Уверенность);
			Иначе
				// Обрабатываем исключения для расходников
				Если ОбработатьИсключения(СтрокаПозиции, ДанныеДляЗаполнения, КатегорияДляРасходников) Тогда
					Продолжить;
				КонецЕсли;
				
				Если Не СтрокаДанныхКВР = Неопределено Тогда
					ЗаполнитьИзДанныхИИ(СтрокаПозиции, СтрокаДанныхКВР, Уверенность);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
	Исключение
		ТекстОшибки = СтрШаблон(
			"Ошибка заполнения результатов для позиции %1: %2",
			ПозицияДляОбработки.НаименованиеПозиции,
			ОписаниеОшибки()
		);
		ЗаписатьВЖурналРегистрации(ТекстОшибки);
	КонецПопытки;
	
КонецПроцедуры

// Получает строку данных КВР в зависимости от источника данных
//
// Параметры:
//  ДанныеДляЗаполнения - Структура, СправочникСсылка - данные от AI (структура) или ссылка на КВР (из кеша)
//  ДанныеВсехПозицийКВР - ТаблицаЗначений - данные классификатора видов работ
//
// Возвращаемое значение:
//  СтрокаТаблицыЗначений, Неопределено - найденная строка данных КВР или Неопределено
//
Функция ПолучитьСтрокуДанныхКВР(ДанныеДляЗаполнения, ДанныеВсехПозицийКВР)
	
	СтрокаДанных = Неопределено;
	
	Если ТипЗнч(ДанныеДляЗаполнения) = Тип("Структура") Тогда
		СтрокаДанных =  НайтиКлассификаторВидовРабот(ДанныеДляЗаполнения, ДанныеВсехПозицийКВР);
	Иначе  
		СтрокаДанных =  ДанныеВсехПозицийКВР.Найти(ДанныеДляЗаполнения, "ВидРаботы"); 
	КонецЕсли;  
	
	Возврат СтрокаДанных;
	
КонецФункции

// Ищет КВР в структуре данных (из кеша или AI)
//
// Параметры:
//  СтруктураДанных - Структура - структура с данными для поиска
//  ДанныеВсехПозицийКВР - ТаблицаЗначений - данные классификатора видов работ
//
// Возвращаемое значение:
//  СтрокаТаблицыЗначений, Неопределено - найденная строка данных КВР или Неопределено
//
Функция НайтиКлассификаторВидовРабот(СтруктураДанных, ДанныеВсехПозицийКВР)
	
    СтрокаДанных = Неопределено;
	
	Если СтруктураДанных.Свойство("КВР") Тогда  
		
		СтрокаДанных = ДанныеВсехПозицийКВР.Найти(СтруктураДанных.КВР, "ВидРаботы");  
		
	КонецЕсли;
	
	Если СтруктураДанных.Свойство("work") Тогда 
		
		СтрокаДанных = ДанныеВсехПозицийКВР.Найти(СтруктураДанных.work, "ВидРаботыНаименование");  
		
	КонецЕсли;
	
	Возврат СтрокаДанных;
	
КонецФункции

// Определяет уровень уверенности в результате сопоставления
//
// Параметры:
//  ДанныеДляЗаполнения - Структура - данные от AI или из кеша
//  СтрокаКеша          - СтрокаТаблицыЗначений, Неопределено - строка из кеша
//
// Возвращаемое значение:
//  Число - уровень уверенности от 0 до 1
//
Функция ОпределитьУверенность(ДанныеДляЗаполнения, СтрокаКеша)
	
	// Если передана структура с явным указанием уверенности (из кеша или AI)
	Если ТипЗнч(ДанныеДляЗаполнения) = Тип("Структура") 
		И ДанныеДляЗаполнения.Свойство("confidence") Тогда
		Возврат ДанныеДляЗаполнения.confidence;
	КонецЕсли;
	
	// Значение по умолчанию
	Возврат 1;
	
КонецФункции

// Заполняет строку позиции данными из кеша
//
// Параметры:
//  СтрокаПозиции   - СтрокаТаблицыЗначений - строка результатов для заполнения
//  СтрокаКеша      - СтрокаТаблицыЗначений - строка из кеша с данными
//  СтрокаДанныхКВР - СтрокаТаблицыЗначений - данные классификатора видов работ
//  Уверенность     - Число - уровень уверенности в результате
//
Процедура ЗаполнитьИзКеша(СтрокаПозиции, СтрокаКеша, СтрокаДанныхКВР, Уверенность)
	
	ЗаполнитьЗначенияСвойств(СтрокаПозиции, СтрокаДанныхКВР);				
	
	СтрокаПозиции.ПолученоИзКеша = Истина;
	СтрокаПозиции.ГоловнаяНоменклатура = СтрокаКеша.ГоловнаяНоменклатура;
	СтрокаПозиции.Категория = СтрокаКеша.Раздел;
	СтрокаПозиции.Уверенность = Уверенность;
	
	Если ПустаяСтрока(СтрокаПозиции.Примечание) Тогда
		СтрокаПозиции.Примечание = "Получено из журнала";
	Иначе
		СтрокаПозиции.Примечание = СтрокаПозиции.Примечание + ". Получено из журнала";
	КонецЕсли;
	
КонецПроцедуры

// Заполняет строку позиции данными, полученными от AI
//
// Параметры:
//  СтрокаПозиции   - СтрокаТаблицыЗначений - строка результатов для заполнения
//  СтрокаДанныхКВР - СтрокаТаблицыЗначений - данные классификатора видов работ
//  Уверенность     - Число - уровень уверенности в результате (от AI)
//
Процедура ЗаполнитьИзДанныхИИ(СтрокаПозиции, СтрокаДанныхКВР, Уверенность)
	
	ЗаполнитьЗначенияСвойств(СтрокаПозиции, СтрокаДанныхКВР);
	СтрокаПозиции.Уверенность = Уверенность;
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСИИ

// Формирует текстовый запрос для AI-сервиса (универсальная для одной или нескольких позиций)
//
// Параметры:
//  ПозицииДляОбработки   - Массив, СтрокаТаблицыЗначений - одна позиция или массив позиций для анализа
//  СписокРаботИКатегорий - ТаблицаЗначений - список работ и категорий для сопоставления
//  МассивКатегорий       - Массив - уникальные категории работ
//  ПапкаРабот            - СправочникСсылка.гк_КлассификаторВидовРабот - группа классификатора
//
// Возвращаемое значение:
//  Строка - сформированный текст запроса для отправки в AI
//
Функция СформироватьЗапросДляИИ(ПозицииДляОбработки, СписокРаботИКатегорий, МассивКатегорий, ПапкаРабот)
	
	// Формируем список работ для анализа
	СписокДляАнализа = СформироватьСписокРаботДляАнализа(СписокРаботИКатегорий, МассивКатегорий);
	
	// Формируем массив данных для анализа
	МассивДанныхДляАнализа = СформироватьМассивДанныхПозиций(ПозицииДляОбработки);
	
	// Преобразуем в JSON
	ДанныеДляАнализаJSON = гк_HTTPКонекторУниверсальный.ЗначениеВСтрокуJSON(
		МассивДанныхДляАнализа, 
		Новый ПараметрыЗаписиJSON(ПереносСтрокJSON.Windows)
	);
	
	// Получаем правила сопоставления
	ПравилаСопоставления = ис_НастройкиИнструментов.ПравилаПапки(ПапкаРабот);
	
	// Получаем шаблон запроса и формируем текст
	ТекстЗапроса = ис_НастройкиИнструментов.ШаблонЗапроса();
	ТекстЗапроса = СтрШаблон(ТекстЗапроса, ДанныеДляАнализаJSON, СписокДляАнализа, ПравилаСопоставления); 
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Формирует список наименований работ для передачи в AI
//
// Параметры:
//  СписокРаботИКатегорий - ТаблицаЗначений - список работ и категорий
//  МассивКатегорий       - Массив - уникальные категории для фильтрации
//
// Возвращаемое значение:
//  Строка - наименования работ, разделенные символами перевода строки
//
Функция СформироватьСписокРаботДляАнализа(СписокРаботИКатегорий, МассивКатегорий)
	
	МассивНаименованийРабот = Новый Массив;
	
	Для Каждого Категория Из МассивКатегорий Цикл
		МассивСтрокРабот = СписокРаботИКатегорий.НайтиСтроки(Новый Структура("Категория", Категория));
		
		Для Каждого СтрокаРаботы Из МассивСтрокРабот Цикл
			МассивНаименованийРабот.Добавить(СтрокаРаботы.ВидРаботы.Наименование);
		КонецЦикла;
	КонецЦикла;
	
	Возврат СтрСоединить(МассивНаименованийРабот, Символы.ПС);
	
КонецФункции

// Формирует массив структур с данными позиций для передачи в AI
//
// Параметры:
//  ПозицииДляОбработки - Массив, СтрокаТаблицыЗначений - одна позиция или массив позиций
//
// Возвращаемое значение:
//  Массив - массив структур с полями:
//   * НаименованиеПозиции - Строка
//   * ТипМарка            - Строка
//   * ЕдиницаИзмерения    - Строка
//   * Завод               - Строка
//   * КодИзделия          - Строка
//
Функция СформироватьМассивДанныхПозиций(ПозицииДляОбработки)
	
	МассивДанныхДляАнализа = Новый Массив;
	
	// Если передана одна позиция (не массив), преобразуем в массив
	Если ТипЗнч(ПозицииДляОбработки) <> Тип("Массив") Тогда
		МассивПозиций = Новый Массив;
		МассивПозиций.Добавить(ПозицииДляОбработки);
	Иначе
		МассивПозиций = ПозицииДляОбработки;
	КонецЕсли;
	
	Для Каждого Позиция Из МассивПозиций Цикл
		ДанныеДляАнализа = НовыеДанныеДляАнализа(); 
		ДанныеДляАнализа.НаименованиеПозиции = Позиция.НаименованиеПозиции;
		ДанныеДляАнализа.ТипМарка = Позиция.ТипМарка;
		ДанныеДляАнализа.ЕдиницаИзмерения = Позиция.ЕдиницаИзмерения; 
		ДанныеДляАнализа.Завод = Позиция.Завод; 
		ДанныеДляАнализа.КодИзделия = Позиция.КодИзделия; 
		
		МассивДанныхДляАнализа.Добавить(ДанныеДляАнализа);
	КонецЦикла;
	
	Возврат МассивДанныхДляАнализа;
	
КонецФункции

// Отправляет запрос к AI-сервису и возвращает распарсенный ответ
//
// Параметры:
//  ТекстЗапроса  - Строка - текст запроса для AI
//  МодельЗапроса - Строка - идентификатор модели AI-сервиса (например, "gpt-4")
//  Токен         - Строка, Неопределено - токен авторизации для AI-сервиса (необязательный)
//
// Возвращаемое значение:
//  Структура, Неопределено - распарсенный JSON-ответ от AI или Неопределено в случае ошибки
//
Функция ОтправитьЗапросChatGPT(ТекстЗапроса)
	
	Попытка
		ОбъектСистемы = Справочники.бит_ОбъектыСистемы.ОбъектСистемыПоОбъектуМетаданных(Метаданные.Обработки.ис_СопоставлениеРаботПоСпецификации);
		
		ОписаниеЗапроса = ai_Запросы.НовоеОписаниеЗапроса(ОбъектСистемы);
		ОписаниеЗапроса.ТекстЗапроса = ТекстЗапроса;
		
		ТекстОтвета = ai_Запросы.ВыполнитьЗапрос(ОписаниеЗапроса);
		
		ДанныеОтвет = гк_HTTPКонекторУниверсальный.ЗначениеИзСтрокиJSON(ТекстОтвета);  
		  
		Возврат ДанныеОтвет;
		
	Исключение
		ТекстОшибки = "Ошибка при отправке запроса к AI: " + ОписаниеОшибки();
		ЗаписатьВЖурналРегистрации(ТекстОшибки);
		Возврат Неопределено;
	КонецПопытки;
	
КонецФункции

// Возвращает контекстную информацию для AI о роли и задаче
//
// Возвращаемое значение:
//  Строка - текст контекста для AI-сервиса
//
Функция КонтекстЗапроса()
	
	Возврат "Ты эксперт по инженерным системам. Анализируй запрос и дай экспертный ответ.";
	
КонецФункции

#КонецОбласти

#Область ВспомогательныеФункции

// Разбивает массив позиций на пакеты заданного размера
//
// Параметры:
//  МассивПозиций - Массив - исходный массив позиций для разбиения
//  РазмерПакета  - Число - количество элементов в одном пакете
//
// Возвращаемое значение:
//  Массив - массив пакетов, где каждый пакет - это массив позиций
//
Функция РазбитьНаПакеты(МассивПозиций, РазмерПакета)
	
	МассивПакетов = Новый Массив;
	ТекПакет = Новый Массив;
	
	Для Каждого Позиция Из МассивПозиций Цикл
		ТекПакет.Добавить(Позиция);
		
		Если ТекПакет.Количество() >= РазмерПакета Тогда
			МассивПакетов.Добавить(ТекПакет);
			ТекПакет = Новый Массив;
		КонецЕсли;
	КонецЦикла;
	
	// Добавляем последний неполный пакет
	Если ТекПакет.Количество() > 0 Тогда
		МассивПакетов.Добавить(ТекПакет);
	КонецЕсли;
	
	Возврат МассивПакетов;
	
КонецФункции

// Создает структуру отбора для поиска строк позиции в таблице результатов
//
// Параметры:
//  Позиция - СтрокаТаблицыЗначений - позиция для создания отбора
//
// Возвращаемое значение:
//  Структура - отбор с ключевыми полями позиции:
//   * НаименованиеПозиции - Строка
//   * Завод               - Строка
//   * КодИзделия          - Строка
//   * ТипМарка            - Строка
//
Функция СоздатьОтборПоПозиции(Позиция)
	
	Отбор = Новый Структура;
	Отбор.Вставить("НаименованиеПозиции", Позиция.НаименованиеПозиции);
	Отбор.Вставить("Завод", Позиция.Завод);
	Отбор.Вставить("КодИзделия", Позиция.КодИзделия);
	Отбор.Вставить("ТипМарка", Позиция.ТипМарка);
	
	Возврат Отбор;
	
КонецФункции

// Ищет позицию в кеше по индексу (оптимизированный поиск O(1))
//
// Параметры:
//  Позиция     - СтрокаТаблицыЗначений - позиция для поиска
//  ИндексКеша  - Соответствие - индекс кеша для быстрого поиска
//
// Возвращаемое значение:
//  СтрокаТаблицыЗначений, Неопределено - найденная строка из кеша или Неопределено
//
Функция НайтиВКешеПоИндексу(Позиция, ИндексКеша)
	
	Ключ = ПолучитьКлючПозиции(
		Позиция.НаименованиеПозиции,
		Позиция.Завод,
		Позиция.КодИзделия,
		Позиция.ТипМарка
	);
	
	СтрокаКеша = ИндексКеша.Получить(Ключ);
	
	Возврат СтрокаКеша;
	
КонецФункции

#Область ГибкийПоиск

// Поиск позиции в кеше с приоритетами и расчетом уверенности
//
// Параметры:
//  НаименованиеПозиции - Строка - наименование позиции
//  Завод               - Строка - завод
//  КодИзделия          - Строка - код изделия
//  ТипМарка            - Строка - тип/марка
//  ИндексКеша          - Структура - многоуровневый индекс кеша
//  ГибкийПоиск         - Булево - использовать гибкий поиск (по всем уровням) или только точное совпадение.
//                                 Значение по умолчанию: Истина
//
// Возвращаемое значение:
//  Структура - результат поиска:
//   * НайденаСтрока - СтрокаТаблицыЗначений, Неопределено - найденная строка из кеша
//   * Уверенность   - Число - уровень уверенности (1.0, 0.9, 0.8, 0.7 или 0)
//   * УровеньПоиска - Число - уровень, на котором найдена запись (1, 2, 3, 4 или 0)
//
Функция НайтиВКеше(НаименованиеПозиции, Завод, КодИзделия, ТипМарка, ИндексКеша, ГибкийПоиск = Истина)
	
	Результат = ИнициализироватьРезультатПоиска();
	
	// Определяем уровни поиска
	УровниПоиска = ПолучитьУровниПоискаВКеше(ГибкийПоиск);
	
	// Последовательный поиск по уровням
	Для Каждого ОписаниеУровня Из УровниПоиска Цикл
		
		// Проверяем наличие индекса для данного уровня
		Если НЕ ИндексКеша.Свойство(ОписаниеУровня.ИмяУровня) Тогда
			Продолжить;
		КонецЕсли;
		
		// Формируем ключ и ищем в индексе
		Ключ = СформироватьКлючПоУровню(НаименованиеПозиции, Завод, КодИзделия, ТипМарка, ОписаниеУровня.Параметры);
		
		СтрокаКеша = ИндексКеша[ОписаниеУровня.ИмяУровня].Получить(Ключ);
		
		Если Не СтрокаКеша = Неопределено Тогда
			
			Результат.НайденаСтрока = СтрокаКеша;
			Результат.Уверенность = ОписаниеУровня.Уверенность;
			Результат.УровеньПоиска = ОписаниеУровня.НомерУровня;
			
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Инициализирует структуру результата поиска в кеше
//
// Возвращаемое значение:
//  Структура - пустой результат поиска
//
Функция ИнициализироватьРезультатПоиска()
	
	Результат = Новый Структура;
	Результат.Вставить("НайденаСтрока", Неопределено);
	Результат.Вставить("Уверенность", 0);
	Результат.Вставить("УровеньПоиска", 0);
	
	Возврат Результат;
	
КонецФункции

// Возвращает описание уровней поиска в кеше
//
// Параметры:
//  ГибкийПоиск - Булево - использовать все уровни или только первый
//
// Возвращаемое значение:
//  Массив из Структура - описания уровней поиска:
//   * ИмяУровня - Строка - имя свойства в индексе ("Уровень1", "Уровень2" и т.д.)
//   * НомерУровня - Число - порядковый номер уровня
//   * Уверенность - Число - уровень уверенности для данного уровня
//   * Параметры - Массив из Строка - используемые параметры для формирования ключа
//
Функция ПолучитьУровниПоискаВКеше(ГибкийПоиск)
	
	УровниПоиска = Новый Массив;
	
	// Уровень 1: всегда используется (4 параметра)
	ПараметрыУровня = СтрРазделить("НаименованиеПозиции,Завод,КодИзделия,ТипМарка", ",");
	УровниПоиска.Добавить(ОписаниеУровняПоиска(1, "Уровень1", 1.0, ПараметрыУровня));
	
	// Дополнительные уровни - только для гибкого поиска
	Если ГибкийПоиск Тогда
		
		// Уровень 2: 3 параметра
		ПараметрыУровня = СтрРазделить("НаименованиеПозиции,КодИзделия,ТипМарка", ",");
		УровниПоиска.Добавить(ОписаниеУровняПоиска(2, "Уровень2", 0.9, ПараметрыУровня));
		
		// Уровень 3: 2 параметра
		ПараметрыУровня = СтрРазделить("НаименованиеПозиции,ТипМарка", ",");
		УровниПоиска.Добавить(ОписаниеУровняПоиска(3, "Уровень3", 0.8, ПараметрыУровня));
		
		// Уровень 4: 1 параметр
		ПараметрыУровня = СтрРазделить("НаименованиеПозиции", ",");
		УровниПоиска.Добавить(ОписаниеУровняПоиска(4, "Уровень4", 0.7, ПараметрыУровня));
		
	КонецЕсли;
	
	Возврат УровниПоиска;
	
КонецФункции

// Создает описание уровня поиска
//
// Параметры:
//  НомерУровня - Число - порядковый номер уровня
//  ИмяУровня - Строка - имя свойства в индексе
//  Уверенность - Число - уровень уверенности
//  Параметры - Массив из Строка - используемые параметры
//
// Возвращаемое значение:
//  Структура - описание уровня поиска
//
Функция ОписаниеУровняПоиска(НомерУровня, ИмяУровня, Уверенность, Параметры)
	
	Описание = Новый Структура;
	Описание.Вставить("НомерУровня", НомерУровня);
	Описание.Вставить("ИмяУровня", ИмяУровня);
	Описание.Вставить("Уверенность", Уверенность);
	Описание.Вставить("Параметры", Параметры);
	
	Возврат Описание;
	
КонецФункции

// Формирует ключ поиска по указанным параметрам
//
// Параметры:
//  НаименованиеПозиции - Строка
//  Завод - Строка
//  КодИзделия - Строка
//  ТипМарка - Строка
//  ИспользуемыеПараметры - Массив из Строка - параметры для ключа
//
// Возвращаемое значение:
//  Строка - сформированный ключ
//
Функция СформироватьКлючПоУровню(НаименованиеПозиции, Завод, КодИзделия, ТипМарка, ИспользуемыеПараметры)
	
	ЗначенияПараметров = Новый Соответствие;
	ЗначенияПараметров.Вставить("НаименованиеПозиции", НаименованиеПозиции);
	ЗначенияПараметров.Вставить("Завод", Завод);
	ЗначенияПараметров.Вставить("КодИзделия", КодИзделия);
	ЗначенияПараметров.Вставить("ТипМарка", ТипМарка);
	
	// Получаем значения только для используемых параметров
	ЗначенияДляКлюча = Новый Массив;
	
	Для Каждого ИмяПараметра Из ИспользуемыеПараметры Цикл
		Значение = ЗначенияПараметров.Получить(ИмяПараметра);
		ЗначенияДляКлюча.Добавить(?(Значение = Неопределено, "", Значение));
	КонецЦикла;
	
	Возврат ПолучитьКлючПозиции(
		ЗначенияДляКлюча[0], // НаименованиеПозиции - всегда первый
		?(ЗначенияДляКлюча.Количество() > 1, ЗначенияДляКлюча[1], ""), // Завод
		?(ЗначенияДляКлюча.Количество() > 2, ЗначенияДляКлюча[2], ""), // КодИзделия
		?(ЗначенияДляКлюча.Количество() > 3, ЗначенияДляКлюча[3], "")  // ТипМарка
	);
	
КонецФункции

#КонецОбласти

// Создает новую структуру с пустыми полями данных позиции для анализа AI
//
// Возвращаемое значение:
//  Структура - структура с полями:
//   * НаименованиеПозиции - Строка - пустая строка
//   * ТипМарка            - Строка - пустая строка
//   * ЕдиницаИзмерения    - Строка - пустая строка
//   * Завод               - Строка - пустая строка
//   * КодИзделия          - Строка - пустая строка
//
Функция НовыеДанныеДляАнализа() 
	
	ДанныеДляАнализа = Новый Структура;
	ДанныеДляАнализа.Вставить("НаименованиеПозиции", "");
	ДанныеДляАнализа.Вставить("ТипМарка", "");
	ДанныеДляАнализа.Вставить("ЕдиницаИзмерения", "");
	ДанныеДляАнализа.Вставить("Завод", "");
	ДанныеДляАнализа.Вставить("КодИзделия", "");
	
	Возврат ДанныеДляАнализа;
	
КонецФункции

// Записывает сообщение об ошибке в журнал регистрации платформы
//
// Параметры:
//  ТекстСообщения - Строка - текст сообщения для записи в журнал
//
Процедура ЗаписатьВЖурналРегистрации(ТекстСообщения)
	
	ЗаписьЖурналаРегистрации(
		"ОбработкаПозицийЧерезИИ",
		УровеньЖурналаРегистрации.Ошибка,
		,
		,
		ТекстСообщения
	);
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСБазойДанных

// Получает данные классификатора видов работ с ценами из шаблонов ССОМ
//
// Параметры:
//  МассивВидовРабот - Массив из СправочникСсылка.гк_КлассификаторВидовРабот - виды работ для загрузки
//
// Возвращаемое значение:
//  ТаблицаЗначений - таблица с полями:
//   * ВидРаботы                  - СправочникСсылка.гк_КлассификаторВидовРабот
//   * ВидРаботыНаименование      - Строка
//   * Кодификатор                - Строка
//   * СтоимостьПрочихМатериалов  - Число
//   * КодСтатьи                  - Строка
//   * ЦенаРабот                  - Число
//   * Категория                  - СправочникСсылка.гк_СтатьиБДДС
//   * ГоловнаяНоменклатура       - СправочникСсылка.Номенклатура
//   * ЕдИзмКВР                   - СправочникСсылка.ЕдиницыИзмерения
//   * ЦенаИндикатив              - Число - индикативная цена из шаблона ССОМ
//
Функция ПолучитьДанныеКВР(МассивВидовРабот) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивКВР", МассивВидовРабот);
	Запрос.УстановитьПараметр("ВидОперации", Перечисления.гк_ВидыШаблонаССОМ.Основной);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	гк_КлассификаторВидовРабот.Ссылка КАК ВидРаботы,
	|	гк_КлассификаторВидовРабот.Наименование КАК ВидРаботыНаименование,
	|	гк_КлассификаторВидовРабот.Кодификатор КАК Кодификатор,
	|	гк_КлассификаторВидовРабот.СтоимостьПрочиеМатериалы КАК СтоимостьПрочихМатериалов,
	|	гк_КлассификаторВидовРабот.СтатьяБДДС.Кодификатор КАК КодСтатьи,
	|	гк_КлассификаторВидовРабот.СтоимостьРаботы КАК ЦенаРабот,
	|	гк_КлассификаторВидовРабот.СтатьяБДДС КАК Категория,
	|	гк_КлассификаторВидовРаботМатериалы.Номенклатура КАК ГоловнаяНоменклатура,
	|	гк_КлассификаторВидовРабот.ЕдиницаИзмерения КАК ЕдИзмКВР
	|ПОМЕСТИТЬ ВТ_ДанныеКВР
	|ИЗ
	|	Справочник.гк_КлассификаторВидовРабот КАК гк_КлассификаторВидовРабот
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.гк_КлассификаторВидовРабот.Материалы КАК гк_КлассификаторВидовРаботМатериалы
	|		ПО гк_КлассификаторВидовРабот.Ссылка = гк_КлассификаторВидовРаботМатериалы.Ссылка
	|		И гк_КлассификаторВидовРаботМатериалы.НомерСтроки = 1
	|ГДЕ
	|	гк_КлассификаторВидовРабот.Ссылка В(&МассивКВР)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	гк_ШаблонССОММатериалы.Материал.гк_ГоловнаяНоменклатура КАК гк_ГоловнаяНоменклатура,
	|	гк_ШаблонССОММатериалы.Цена КАК Цена
	|ПОМЕСТИТЬ ВТ_ЦеныГоловнойНоменклатуры
	|ИЗ
	|	Документ.гк_ШаблонССОМ.Материалы КАК гк_ШаблонССОММатериалы
	|ГДЕ
	|	гк_ШаблонССОММатериалы.Ссылка.ВидОперации = &ВидОперации
	|	И гк_ШаблонССОММатериалы.Материал.гк_ГоловнаяНоменклатура В
	|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				ВТ_ДанныеКВР.ГоловнаяНоменклатура
	|			ИЗ
	|				ВТ_ДанныеКВР)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеКВР.ВидРаботы КАК ВидРаботы,
	|	ВТ_ДанныеКВР.ВидРаботыНаименование КАК ВидРаботыНаименование,
	|	ВТ_ДанныеКВР.Кодификатор КАК Кодификатор,
	|	ВТ_ДанныеКВР.СтоимостьПрочихМатериалов КАК СтоимостьПрочихМатериалов,
	|	ВТ_ДанныеКВР.КодСтатьи КАК КодСтатьи,
	|	ВТ_ДанныеКВР.ЦенаРабот КАК ЦенаРабот,
	|	ВТ_ДанныеКВР.Категория КАК Категория,
	|	ВТ_ДанныеКВР.ГоловнаяНоменклатура КАК ГоловнаяНоменклатура,
	|	ВТ_ДанныеКВР.ЕдИзмКВР КАК ЕдИзмКВР,
	|	ВТ_ЦеныГоловнойНоменклатуры.Цена КАК ЦенаИндикатив
	|ИЗ
	|	ВТ_ДанныеКВР КАК ВТ_ДанныеКВР
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЦеныГоловнойНоменклатуры КАК ВТ_ЦеныГоловнойНоменклатуры
	|		ПО ВТ_ДанныеКВР.ГоловнаяНоменклатура = ВТ_ЦеныГоловнойНоменклатуры.гк_ГоловнаяНоменклатура";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Получает таблицу кешированных ответов AI из регистра сведений
//
// Параметры:
//  ТЗУникальныеПозиции - ТаблицаЗначений - таблица уникальных позиций для отбора кеша
//  МассивРазделов      - Массив - массив разделов (категорий работ) для фильтрации кеша.
//                                 Если не указан или пустой - фильтрация не применяется
//
// Возвращаемое значение:
//  ТаблицаЗначений - таблица кеша с полями:
//   * КВР                   - СправочникСсылка.гк_КлассификаторВидовРабот
//   * Завод                 - Строка
//   * КодИзделия            - Строка
//   * ТипМарка              - Строка
//   * Раздел                - СправочникСсылка.гк_СтатьиБДДС
//   * ГоловнаяНоменклатура  - СправочникСсылка.Номенклатура
//   * ЕдиницаИзмерения      - СправочникСсылка.ЕдиницыИзмерения
//   * НаименованиеПозиции   - Строка
//
Функция ПолучитьТаблицуКеша(УникальныеПозиции, МассивРазделов = Неопределено)

	НаименованияПозиций = УникальныеПозиции.ВыгрузитьКолонку("НаименованиеПозиции");

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НаименованияПозиций", НаименованияПозиций);

	// Определяем нужна ли фильтрация по разделам
	ИспользоватьФильтрПоРазделам = (МассивРазделов <> Неопределено) И (МассивРазделов.Количество() > 0);

	Если ИспользоватьФильтрПоРазделам Тогда
		Запрос.УстановитьПараметр("МассивРазделов", МассивРазделов);
	КонецЕсли;

	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	КЕШ.КВР,
	|	КЕШ.Завод,
	|	КЕШ.КодИзделия,
	|	КЕШ.ТипМарка,
	|	КЕШ.Раздел,
	|	КЕШ.ГоловнаяНоменклатура,
	|	КЕШ.ЕдиницаИзмерения,
	|	КЕШ.НаименованиеПозиции
	|ИЗ
	|	РегистрСведений.КешОтветовИИ КАК КЕШ
	|ГДЕ
	|	КЕШ.НаименованиеПозиции В(&НаименованияПозиций)"
	+ ?(ИспользоватьФильтрПоРазделам, "
	|	И КЕШ.Раздел В(&МассивРазделов)", "");

	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции

Функция ПолучитьКатегориюДляРасходников(ПапкаРабот)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	гк_КлассификаторВидовРабот.СтатьяБДДС КАК Категория,
	|	гк_КлассификаторВидовРабот.СтатьяБДДС.Наименование КАК КатегорияНаименование
	|ИЗ
	|	Справочник.гк_КлассификаторВидовРабот КАК гк_КлассификаторВидовРабот
	|ГДЕ
	|	НЕ гк_КлассификаторВидовРабот.ПометкаУдаления
	|	И гк_КлассификаторВидовРабот.Ссылка В ИЕРАРХИИ(&Группа)
	|	И НЕ гк_КлассификаторВидовРабот.ЭтоГруппа
	|	И гк_КлассификаторВидовРабот.Кодификатор ПОДОБНО ""%.000""";
	
	Запрос.УстановитьПараметр("Группа", ПапкаРабот);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Категория;
	Иначе
		Возврат Неопределено;	
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область ОбработкаИсключений

// Обработка специальных случаев (расходников)
//
Функция ОбработатьИсключения(СтрокаПозиции, СтруктураОтветаИИ, КатегорияДляРасходников)
	
	Если ТипЗнч(СтруктураОтветаИИ) <> Тип("Структура") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если СтруктураОтветаИИ.Свойство("consumables") Тогда
		Если НРег(СтруктураОтветаИИ.consumables) = "да" Тогда
			СтрокаПозиции.ВидРаботыНаименование = "Расходники";
			СтрокаПозиции.Категория = КатегорияДляРасходников;
			
			Если КатегорияДляРасходников <> Неопределено Тогда
				СтрокаПозиции.КодСтатьи = КатегорияДляРасходников.Кодификатор;
			КонецЕсли;
			
			СтрокаПозиции.Уверенность = 0;
			
			Возврат Истина;
		КонецЕсли;	
	КонецЕсли;
	
	Возврат Ложь; 
	
КонецФункции

#КонецОбласти

#КонецОбласти